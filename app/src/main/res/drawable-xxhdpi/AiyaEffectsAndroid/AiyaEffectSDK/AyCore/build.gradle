apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.wuwang.aavt'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode rootProject.ext.vCode
        versionName rootProject.ext.vName

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
                arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=gnustl_static'
            }
        }
        ndk{
            abiFilters "armeabi-v7a", "arm64-v8a", "x86"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
    sourceSets{
        main{
            jniLibs.srcDirs+="libs"
        }
    }
}

task buildMyJar(type: Jar, dependsOn: ['build']) {
    //导出的jar文件名称
    archiveName = 'AySdkCore.jar'
    //从哪个目录打包jar
    from('build/intermediates/classes/release/')
    destinationDir = file('../output/core/')
    //去掉不要的类
    exclude('**/BuildConfig.class')
    exclude('**/BuildConfig\$*.class')
    exclude('**/R.class')
    exclude('**/R\$*.class')
    include('com/aiyaapp/aiya/**')

    doLast{
        copy {
            from('libs')
            into('../output/core/')
        }
        copy {
            from('build/intermediates/cmake/release/obj')
            into('../output/core/')
        }
    }
}

task buildJavaDoc(type:Javadoc){
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
    source 'src/main/java'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

apply from: "buCore.gradle"
