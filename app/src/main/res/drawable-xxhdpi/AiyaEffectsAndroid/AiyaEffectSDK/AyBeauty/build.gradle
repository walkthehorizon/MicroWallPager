apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode rootProject.ext.vCode
        versionName rootProject.ext.vName

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86"
        }
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
                arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=gnustl_static'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs += "libs"
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

task buildMyJar(type: Jar, dependsOn: ['build']) {
    //导出的jar文件名称
    archiveName = 'AyBeauty.jar'
    //从哪个目录打包jar
    from('build/intermediates/classes/release/')
    destinationDir = file('../output/beauty/')
    //去掉不要的类
    exclude('**/BuildConfig.class')
    exclude('**/BuildConfig\$*.class')
    exclude('**/R.class')
    exclude('**/R\$*.class')
    include('com/aiyaapp/aiya/**')

    doLast {
        copy {
            from('libs/', 'build/intermediates/cmake/release/obj/')
            into('../output/beauty/')
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //compile project(':AyCore')
    implementation project(':aavt')
}

apply from: "buBeauty.gradle"
